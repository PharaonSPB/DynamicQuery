group 'com.github.dsulimchuk.dynamicquery'
version '1.0.8-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.5.32'
    ext.dokka_version = "1.5.31"
    ext.detekt_version = '1.0.0.RC6-4'

    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
        classpath "gradle.plugin.io.gitlab.arturbosch.detekt:detekt-gradle-plugin:${detekt_version}"
    }
}

apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'io.gitlab.arturbosch.detekt'

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

detekt {
    version = detekt_version
    profile("main") {
        input = "$projectDir/src/main/kotlin"
        config = "$projectDir/detekt.yml"
        filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
    }
}

signing {
    sign publishing.publications
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "io.github.microutils:kotlin-logging-jvm:2.0.13"
    implementation "jakarta.persistence:jakarta.persistence-api:2.2.3"
    implementation 'org.springframework:spring-beans:5.2.17.RELEASE'
    implementation 'org.apache.commons:commons-lang3:3.12.0'


    testImplementation(platform("org.junit:junit-bom:5.8.2"))
    testRuntimeOnly("org.junit.platform:junit-platform-launcher") {
        because("Only needed to run tests in a version of IntelliJ IDEA that bundles older versions")
    }
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation 'org.junit.vintage:junit-vintage-engine:5.8.2'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation "ch.qos.logback:logback-classic:1.2.10"
    testImplementation 'jakarta.xml.bind:jakarta.xml.bind-api:2.3.3'
    testImplementation 'org.hibernate:hibernate-core:5.4.33'
    testImplementation 'com.h2database:h2:1.4.199'
}

test {
    useJUnitPlatform {
        //includeTags("fast", "smoke & feature-a")
        // excludeTags("slow", "ci")
        //includeEngines("junit-jupiter")
        includeEngines("junit-vintage")
        // excludeEngines("junit-vintage")
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = 'dynamic-query'
            version = version
            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = artifactId
                description = 'Dynamic Query DSL for JPA (Hibernate) on Kotlin'
                url = 'https://github.com/dsulimchuk/DynamicQuery'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id = 'dsulimchuk'
                        name = 'Dmitrii Sulimchuk'
                        email = 'dserge.spb@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/dsulimchuk/DynamicQuery.git'
                    developerConnection = 'scm:git:ssh://git@github.com:dsulimchuk/DynamicQuery.git'
                    url = 'https://github.com/dsulimchuk/DynamicQuery'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"

            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = findProperty('ossrhUsername')
                password = findProperty('ossrhPassword')
            }
        }
    }
}
